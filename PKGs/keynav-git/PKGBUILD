# Maintainer: David Sierra DiazGranados <davidsierradz at gmail dot com>
# Contributor: Rastislav Barlik <barlik-arch at gmx dot com>
# Contributor: N. Izumi - izmntuk
# Contributor: Kyle Keen <keenerd@gmail.com>
# Contributor: Jan Fader <jan.fader@web.de>
pkgname=keynav-git
pkgver=0.+r285.20190221
pkgrel=1
pkgdesc="Ingenious and fast way to move the mouse pointer on the screen with keystrokes."
url="http://www.semicomplete.com/projects/keynav/"
license=('BSD')
arch=('i686' 'x86_64')
groups=('modified')
depends=('xdotool' 'cairo')
makedepends=('git')
source=('git+https://github.com/yjftsjthsd-g/keynav')
sha1sums=('SKIP')

pkgver() {
	cd "${srcdir}/${pkgname%%-*}"
	echo '0.+r'$(git rev-list --count HEAD).$(git log -1 --pretty='%cd' --date=short | sed 's|-||g')
}

build() {
	cd "${srcdir}/${pkgname%%-*}"
	make keynav
	make keynav.1
}

package() {
	cd "${srcdir}/${pkgname%%-*}"
	install -D keynav "$pkgdir/usr/bin/keynav"
	install -D keynavrc "$pkgdir/etc/keynav/keynavrc"
	install -D keynav.1 "$pkgdir/usr/share/man/man1/keynav.1"
}
