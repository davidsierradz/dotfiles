# Maintainer: David Sierra DiazGranados <davidsierradz at gmail dot com>
_name='zsh-autosuggestions'
pkgname="${_name}-git"
pkgver=v0.6.3.r6.g43f3bc4
pkgrel=1
pkgdesc='Fish shell like fast/unobtrusive autosuggestions for zsh'
url='https://github.com/zsh-users/zsh-autosuggestions'
arch=('any')
groups=('modified')
license=('Custom:MIT')
depends=('zsh' 'oh-my-zsh-git')
makedepends=('git')
provides=('zsh-autosuggestions')
source=("${_name}::${url//https/git}")
sha256sums=('SKIP')

pkgver() {
     cd "${srcdir}/${_name}"
     git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

package() {
    cd "${srcdir}/${_name}"
    install -d "${pkgdir}/usr/share/oh-my-zsh/custom/plugins/${_name}"
    install -Dm644 "zsh-autosuggestions"{.plugin,}".zsh" "${pkgdir}/usr/share/oh-my-zsh/custom/plugins/${_name}"

    # license
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
}
