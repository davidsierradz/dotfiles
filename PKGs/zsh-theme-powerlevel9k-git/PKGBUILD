# Maintainer: David Sierra DiazGranados <davidsierradz at gmail dot com>
# Contributor: Florian Dejonckheere <florian at floriandejonckheere dot be>
# Contributor:  jyantis <yantis@yantis.net>
# Contributor: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>

_pkgname=powerlevel9k
pkgname=zsh-theme-${_pkgname}-git
pkgver=v0.6.4.r45.gafb7387
pkgrel=1
pkgdesc='Powerlevel9k theme for zsh'
groups=('modified')
arch=('any')
url='https://github.com/bhilburn/powerlevel9k'
license=('MIT')
depends=(
  'zsh'
  'oh-my-zsh-git')
optdepends=(
  'powerline-fonts: patched fonts for powerline'
  'prezto-git: Prezto integration'
  'antigen-git: Antigen integration'
  'zpm: ZPM integration'
  'zsh-zim-git: Zim integration'
  'awesome-terminal-fonts: icon package'
  'acpi: battery monitoring'
  'git: status of repository'
  'mercurial: status of repository'
  'systemd: virtualization detection'
  'openssh: ssh detection')
source=("${_pkgname}::git+https://github.com/bhilburn/${_pkgname}.git")
sha256sums=('SKIP')
makedepends=('git')
provides=("zsh-theme-${_pkgname}")
conflicts=("zsh-theme-${_pkgname}")
install="zsh-theme-${_pkgname}.install"

pkgver()
{
  cd "${srcdir}/${_pkgname}"
  git describe --long | sed -r 's/([^-]*-g)/r\1/;s/-/./g' ||
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package()
{
  cd "${srcdir}/${_pkgname}"

  # Install license
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Install Documentation
  install -D -m644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"

  # Install the theme
  install -D -m644 "${_pkgname}.zsh-theme" "${pkgdir}/usr/share/oh-my-zsh/custom/themes/${_pkgname}/${_pkgname}.zsh-theme"

  # Install the utilities
  for FILE in functions/*.zsh; do
    install -D -m644 "${FILE}" "${pkgdir}/usr/share/oh-my-zsh/custom/themes/${_pkgname}/functions/$(basename ${FILE})"
  done
}
